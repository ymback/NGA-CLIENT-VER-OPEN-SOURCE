apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def static getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmm')
    return formattedDate

}

android {
    compileSdkVersion project.compileSdkVersion

    signingConfigs {
        release {
            keyAlias 'jgw'
            keyPassword '123456'
            storeFile file(jks_path)
            storePassword '123456'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }

    defaultConfig {
        applicationId "gov.anzong.androidnga"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        multiDexEnabled true
        resConfigs "zh", "en"

        versionCode project.appVersionCode
        versionName project.appVersionName

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

        ndk {
            abiFilters 'arm64-v8a'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            minifyEnabled false
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled false
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (outputFile != null && outputFileName.endsWith('.apk') && variant.buildType.name == "release") {
                outputFileName = "nga_phone_${defaultConfig.versionName}_${getDate()}.apk"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation(name: 'floatingactionmenu', ext: 'aar')

    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.google.android.material:material:$androidxMaterial"
    implementation "androidx.cardview:cardview:1.0.0"

    // lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"

    //butterKnife
    implementation 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.squareup.retrofit2:retrofit-converters:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'
    implementation 'com.alibaba:arouter-api:1.2.4'
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    implementation 'com.github.markzhai:blockcanary-no-op:1.5.0'
    implementation'com.github.daniel-stoneuk:material-about-library:2.3.0'
    implementation project(':lib_common')
    implementation project(':lib_cloud')
    implementation project(':lib_core')
    implementation project(":lib_network")

    // database
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // kotlin
    implementation "androidx.core:core-ktx:1.3.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    //noinspection KtxExtensionAvailable
    implementation "androidx.preference:preference:$preference_version"
}

repositories {
    mavenCentral()
}