apply plugin: 'com.android.application'

static def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmm')
    return formattedDate
}

def isJenkins() {
    return project.hasProperty('IS_JENKINS') && IS_JENKINS == 'true'
}

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    signingConfigs {
        signingConfig {
            if (isJenkins()) {
                storeFile file('../../android19910914.keystore')
            } else {
                storeFile file('..\\..\\android19910914.keystore')
            }
            storePassword '19910914'
            keyAlias 'android.keystore'
            keyPassword '19910914'
        }
    }

    packagingOptions {
        exclude 'lib/arm64-v8a/*.so'
        exclude 'lib/x86_64/*.so'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
    }

    defaultConfig {
        applicationId "gov.anzong.androidnga"
        minSdkVersion 21
        targetSdkVersion 26
        multiDexEnabled true
        resConfigs "zh", "en"

        versionCode project.appVersionCode
        versionName project.appVersionName

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            minifyEnabled false
            zipAlignEnabled true
        }

        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }

      //  instrumentTest.setRoot('tests')

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    //修改生成的apk名字
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def newName
            def timeNow
            def outDirectory
            if (isJenkins()) {
                timeNow = JENKINS_TIME
                outDirectory = '/auto-build/tomcat/webapps/download/'
                newName = 'NGA-v' + APP_VERSION_NAME + '-' + timeNow + '-' + variant.buildType.name + '.apk'
                outputFileName = new File(outDirectory, newName)
            } else {
                timeNow = getDate()
                outDirectory = output.outputFile.parent
                if (variant.buildType.name == 'debug') {
                    newName = 'NGA-v' + project.appVersionName + '-debug.apk'
                } else {
                    newName = 'NGA-v' + project.appVersionName + '-' + timeNow + '-' + variant.buildType.name + '.apk'
                }
                outputFileName = new File(newName)
            }

        }
    }
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    implementation project(':SwipeBackLayoutLibrary')
    implementation project(':ActionBar-PullToRefresh')
    implementation project(':ActionBar-PullToRefresh-Library')
    //compile project(':meizi')
    implementation(name: 'floatingactionmenu', ext: 'aar')
    implementation 'com.squareup.okhttp3:okhttp:3.8.0'
    implementation 'com.alibaba:fastjson:1.2.41'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation "com.android.support:support-v4:" + project.androidSupportVersion
    implementation "com.android.support:design:" + project.androidSupportVersion
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.squareup.retrofit2:retrofit-converters:2.3.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.github.chrisbanes:photoview:2.1.3'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'
    implementation 'com.alibaba:arouter-api:1.2.4'
    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
    implementation 'com.github.bumptech.glide:glide:4.3.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.3.1'
    implementation 'com.android.support:cardview-v7:'  + project.androidSupportVersion
}